{
  parserClass="com.github.alxmag.intellijfakersupport.lang.FakerParser"
  parserUtilClass="com.github.alxmag.intellijfakersupport.lang.FakerParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Faker"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.alxmag.intellijfakersupport.lang.psi"
  psiImplPackage="com.github.alxmag.intellijfakersupport.lang.psi.impl"

  psiImplUtilClass="com.github.alxmag.intellijfakersupport.lang.psi.impl.FakerPsiImplUtil"

  elementTypeHolderClass="com.github.alxmag.intellijfakersupport.lang.psi.FakerTypes"
  elementTypeClass="com.github.alxmag.intellijfakersupport.lang.psi.FakerElementType"
  tokenTypeClass="com.github.alxmag.intellijfakersupport.lang.psi.FakerTokenType"

  tokens = [
    REGULAR_STRING_PART="regexp:(([^\\\"\#]) | (#[^{]))"
    EXPRESSION_LBRACE='#{'
    EXPRESSION_RBRACE='}'

    // PARAM_BEGIN = "'"
    // PARAM_END = "'"

    DOT='.'
    COMMA=','

    PARAMS_LIST_SEPARATOR="regexp:(\s)+"

    IDENTIFIER="regexp:[a-zA-Z]+[a-zA-Z0-9]+"
  ]

  extends("l1Template|l2Template|l3Template")=template
  implements("l1Expression|l2Expression")="com.github.alxmag.intellijfakersupport.lang.psi.impl.FakerParametrizedExpression"
  implements("l3Expression")="com.github.alxmag.intellijfakersupport.lang.psi.impl.FakerExpression"
  extends("l1ParamList|l2ParamList")=paramList
}

fakerFile ::= l1Template

///// L1 /////
l1Template ::= (l1Expression | content)* {
  methods=[ expressions="l1Expression" ]
}
l1Expression ::= EXPRESSION_LBRACE (functionName l1ParamsListDeclaration?)? EXPRESSION_RBRACE {
  pin=1
  name="Expression"
  methods=[
    paramList="l1ParamList"
    getParamQuoteSymbol
  ]
}
functionName ::= functionNameSegment (DOT functionNameSegment)* {
  methods=[
    getFunctionName
    segments="functionNameSegment"
  ]
}
private l1ParamsListDeclaration ::= PARAMS_LIST_SEPARATOR l1ParamList {
  pin=1
}
l1ParamList ::= l1ParamDeclaration* {
  methods=[ params="l1Param" ]
}
private l1ParamDeclaration ::= l1Param (','|&'}') {
  pin=1
  recoverWhile=not_rbrace_or_next_l1ParamDeclaration
}
l1Param ::= PARAM_BEGIN l2Template PARAM_END {
  pin=1
  name="Single-quoted parameter"
  methods=[
    value="l2Template"
    getQuoteSymbol
  ]
  implements="com.github.alxmag.intellijfakersupport.lang.psi.impl.FakerParam"
}

///// L2 /////
l2Template ::= (l2Expression | content)* {
  methods=[ expressions="l2Expression" ]
}
l2Expression ::= EXPRESSION_LBRACE (functionName l2ExpressionParamsList?)? EXPRESSION_RBRACE {
  pin=1
  name="Nested expression"
  methods=[
    paramList="l2ParamList"
    getParamQuoteSymbol
  ]
}
private l2ExpressionParamsList ::= PARAMS_LIST_SEPARATOR l2ParamList {
  pin=1
}
l2ParamList ::= l2ParamDeclaration*
private l2ParamDeclaration ::= l2Param (','|&'}') {
  pin=1
  recoverWhile=not_rbrace_or_next_l2ParamDeclaration
}
// #{top #{nested ''<thisParam>''}}
l2Param ::= PARAM_DOUBLE_QUOTE_BEGIN l3Template PARAM_DOUBLE_QUOTE_END {
  pin=1
  name="Double apostrophe parameter"
  methods=[
    value="l3Template"
    getQuoteSymbol
  ]
  implements="com.github.alxmag.intellijfakersupport.lang.psi.impl.FakerParam"
}

///// L3 /////
l3Template ::= (l3Expression | content)* {
  methods=[ expressions="l3Expression" ]
}
// L3 Expression has no params
l3Expression ::= EXPRESSION_LBRACE functionName? EXPRESSION_RBRACE {
  pin=1
  name="Nested expression"
}

///// Common /////
functionNameSegment ::= IDENTIFIER {
  methods=[ getReference ]
}
private content ::= REGULAR_STRING_PART
fake template ::=
fake paramList ::=

///// Recover rules /////
private not_rbrace_or_next_l1ParamDeclaration ::= !('}'|l1ParamDeclaration)
private not_rbrace_or_next_l2ParamDeclaration ::= !('}'|l2ParamDeclaration)